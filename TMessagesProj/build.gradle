apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:support-v4:23.2.1'
    compile 'com.android.support:support-v4:23.0.1'
    compile "com.google.android.gms:play-services-gcm:8.4.0"
    compile "com.google.android.gms:play-services-maps:8.4.0"
    compile 'net.hockeyapp.android:HockeySDK:3.6.+'
    compile 'com.googlecode.mp4parser:isoparser:1.0.+'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile ('junit:junit:4.12'){
        exclude module: 'support-annotations'
    }
    androidTestCompile ('com.android.support.test:runner:0.4.1'){
        exclude module: 'support-annotations'
    }
    androidTestCompile ('com.android.support.test:rules:0.4.1'){
        exclude module: 'support-annotations'
    }
    // Optional -- Hamcrest library
    androidTestCompile ('org.hamcrest:hamcrest-library:1.3'){
        exclude module: 'support-annotations'
    }
    // Optional -- UI testing with Espresso
    androidTestCompile ('com.android.support.test.espresso:espresso-core:2.2.1'){
        exclude module: 'support-annotations'
    }
    // Optional -- UI testing with UI Automator
}

android {
    testBuildType "debug"

    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    useLibrary 'org.apache.http.legacy'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        debug {
            storeFile file("config/release.keystore")
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }

        release {
            storeFile file("config/release.keystore")
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {

            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.debug
            applicationIdSuffix ".beta"
        }

        release {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        foss {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.release
        }
    }



    sourceSets.main {
        jniLibs.srcDir 'libs'
        jni.srcDirs = [] //disable automatic ndk-build call
    }

    sourceSets.debug {
        manifest.srcFile 'config/debug/AndroidManifest.xml'
    }

    sourceSets.release {
        manifest.srcFile 'config/release/AndroidManifest.xml'
    }

    sourceSets.foss {
        manifest.srcFile 'config/foss/AndroidManifest.xml'
    }



    productFlavors {
        x86 {
            ndk {
                abiFilter "x86"
            }
            versionCode = 2
        }
        arm {
            ndk {
                abiFilter "armeabi"
            }
            versionCode = 0
        }
        armv7 {
            ndk {
                abiFilter "armeabi-v7a"
            }
            versionCode = 1
        }
        fat {
            versionCode = 3
        }
    }

    defaultConfig {
        applicationId = "org.telegram.messenger"
        versionCode = 767
        minSdkVersion 9
        targetSdkVersion 23
        versionName "3.7.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    applicationVariants.all { variant ->
        def abiVersion = variant.productFlavors.get(0).versionCode
        variant.mergedFlavor.versionCode = defaultConfig.versionCode * 10 + abiVersion;
    }
}
